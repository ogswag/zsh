# Clone Moarram/headline if not already present
if [[ ! -d ~/.config/zsh/headline ]]; then
    echo "Cloning Moarram/headline into ~/.config/zsh/headline..."
    mkdir -p ~/.config/zsh
    git clone https://github.com/Moarram/headline.git ~/.config/zsh/headline
    if [[ $? -eq 0 ]]; then
        echo "Successfully cloned headline."
    else
        echo "Failed to clone headline. Please check your internet or Git configuration."
    fi
fi
source ~/.config/zsh/headline/headline.zsh-theme

HL_SEP_MODE='on'

HL_LAYOUT_TEMPLATE=(
  _PRE    "${IS_SSH+ %{$reset$yellow$bold%\}[ssh] }"
  USER    '...'
  HOST    '...'
  VENV    ' ...'
  PATH    ' in ...'
  _SPACER '' # special, only shows when compact, otherwise fill with space
  BRANCH  ' ...'
  STATUS  ' (...)'
  _POST   ''
)
HL_CONTENT_TEMPLATE=(
  USER   "%{$bold$blue%}..."
  HOST   "%{$bold$blue%}@..."
  VENV   "%{$bold$green%}(...)"
  PATH   "%{$bold$red%}..."
  BRANCH "%{$bold$green%}..."
  STATUS "%{$bold$yellow%}..."
)
HL_PROMPT='❯ ' # consider '%#'
HL_GIT_COUNT_MODE='on'
HL_GIT_SEP_SYMBOL='|'
HL_GIT_STATUS_SYMBOLS[STAGED]="%{$green%}+"
HL_GIT_STATUS_SYMBOLS[CHANGED]="%{$yellow%}!"
HL_GIT_STATUS_SYMBOLS[CONFLICTS]="%{$red%}X"
HL_GIT_STATUS_SYMBOLS[CLEAN]="%{$green%}✓"
HL_ERR_MODE='detail'

source ~/.zsh_aliases

export EDITOR='vim'
export SUDO_EDITOR='vim'
export VISUAL='vim'

mkcd () {
    mkdir -p "$1";
    cd "$1"
}


if [[ $os == "Linux" ]]; then
    export LD_LIBRARY_PATH=/usr/local/lib
elif [[ $os == "Darwin" ]]; then
    export PATH="$(brew --prefix)/bin:$PATH"
    export PATH="/usr/local/opt/jpeg/bin:$PATH"
    export PATH="$HOME/.local/bin:$PATH"
    export PATH="/usr/local/sbin:$PATH"
    export PATH="/usr/local/opt/llvm/bin:$PATH"
    export PATH="/opt/local/bin:/opt/local/sbin:$PATH"
else
    echo "Unknown operating system."
fi

##? Clone a plugin, identify its init file, source it, and add it to your fpath.
function plugin-load {
  local repo plugdir initfile initfiles=()
  : ${ZPLUGINDIR:=${ZDOTDIR:-~/.config/zsh}/.zsh_plugins}
  for repo in $@; do
    plugdir=$ZPLUGINDIR/${repo:t}
    initfile=$plugdir/${repo:t}.plugin.zsh
    if [[ ! -d $plugdir ]]; then
      echo "Cloning $repo..."
      git clone -q --depth 1 --recursive --shallow-submodules \
        https://github.com/$repo $plugdir
    fi
    if [[ ! -e $initfile ]]; then
      initfiles=($plugdir/*.{plugin.zsh,zsh-theme,zsh,sh}(N))
      (( $#initfiles )) || { echo >&2 "No init file '$repo'." && continue }
      ln -sf $initfiles[1] $initfile
    fi
    fpath+=$plugdir
    (( $+functions[zsh-defer] )) && zsh-defer . $initfile || . $initfile
  done
}

# where do you want to store your plugins?
ZPLUGINDIR=${ZPLUGINDIR:-${ZDOTDIR:-$HOME/.config/zsh}/.zsh_plugins}

# get zsh_unplugged and store it with your other plugins
if [[ ! -d $ZPLUGINDIR/zsh_unplugged ]]; then
  git clone --quiet https://github.com/mattmc3/zsh_unplugged $ZPLUGINDIR/zsh_unplugged
fi
source $ZPLUGINDIR/zsh_unplugged/zsh_unplugged.zsh

# make list of the Zsh plugins you use
repos=(
  zdharma-continuum/fast-syntax-highlighting
  zsh-users/zsh-history-substring-search
  ael-code/zsh-colored-man-pages
)

# now load your plugins
plugin-load $repos

# zsh-history-substring-search configuration
bindkey '^[[A' history-substring-search-up # or '\eOA'
bindkey '^[[B' history-substring-search-down # or '\eOB'
HISTORY_SUBSTRING_SEARCH_ENSURE_UNIQUE=1
