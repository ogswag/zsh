function plugin-load {
    local repo plugdir initfile initfiles=()
    : ${ZPLUGINDIR:=${ZDOTDIR:-~/.config/zsh}/.zsh_plugins}
    for repo in $@; do
        plugdir=$ZPLUGINDIR/${repo:t}
        initfile=$plugdir/${repo:t}.plugin.zsh
    if [[ ! -d $plugdir ]]; then
        echo "Cloning $repo..."
        git clone -q --depth 1 --recursive --shallow-submodules \
        https://github.com/$repo $plugdir
    fi
    if [[ ! -e $initfile ]]; then
        initfiles=($plugdir/*.{plugin.zsh,zsh-theme,zsh,sh}(N))
        (( $#initfiles )) || { echo >&2 "No init file '$repo'." && continue }
        ln -sf $initfiles[1] $initfile
    fi
    fpath+=$plugdir
    (( $+functions[zsh-defer] )) && zsh-defer . $initfile || . $initfile
done
}

ZPLUGINDIR=${ZPLUGINDIR:-${ZDOTDIR:-$HOME/.config/zsh}/.zsh_plugins}

if [[ ! -d $ZPLUGINDIR/zsh_unplugged ]]; then
    git clone --quiet https://github.com/mattmc3/zsh_unplugged $ZPLUGINDIR/zsh_unplugged
fi
source $ZPLUGINDIR/zsh_unplugged/zsh_unplugged.zsh

repos=(
    zdharma-continuum/fast-syntax-highlighting
    zsh-users/zsh-history-substring-search
    ael-code/zsh-colored-man-pages
    urbainvaes/fzf-marks
)
plugin-load $repos
# zsh-history-substring-search configuration
bindkey '\eOA' history-substring-search-up # or '\eOA'
bindkey '\eOB' history-substring-search-down # or '\eOB'
HISTORY_SUBSTRING_SEARCH_ENSURE_UNIQUE=1
